plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.github.mac-comp127'

// External dependencies

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
  }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.+'
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
    test.resources.srcDirs = ['test']
}

// Test and doc options

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
    afterTest { desc, result -> 
        logger.quiet "[${desc.className}] ${desc.name}: ${result.resultType}"
    }
}

javadoc {
    destinationDir = file("${projectDir}/docs")
    options.addBooleanOption 'Xdoclint:all,-missing', true
}

// Maven artifacts

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
